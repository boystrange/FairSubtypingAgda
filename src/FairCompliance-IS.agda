-- MIT License

-- Copyright (c) 2021 Luca Ciccone and Luca Padovani

-- Permission is hereby granted, free of charge, to any person
-- obtaining a copy of this software and associated documentation
-- files (the "Software"), to deal in the Software without
-- restriction, including without limitation the rights to use,
-- copy, modify, merge, publish, distribute, sublicense, and/or sell
-- copies of the Software, and to permit persons to whom the
-- Software is furnished to do so, subject to the following
-- conditions:

-- The above copyright notice and this permission notice shall be
-- included in all copies or substantial portions of the Software.

-- THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND,
-- EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES
-- OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND
-- NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT
-- HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY,
-- WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING
-- FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR
-- OTHER DEALINGS IN THE SOFTWARE.

{-# OPTIONS --guardedness #-}

open import Data.Product
open import Data.Empty
open import Data.Sum
open import Data.Vec
open import Data.Fin
open import Data.Bool renaming (Bool to ğ”¹)
open import Relation.Unary using (_âˆˆ_; _âŠ†_; _âˆ‰_)
open import Relation.Binary.Definitions
open import Relation.Binary.Construct.Closure.ReflexiveTransitive
open import Relation.Binary.PropositionalEquality
open import Relation.Nullary

open import Common

open import is-lib.InfSys

module FairCompliance-IS {ğ•‹ : Set} (message : Message ğ•‹) where
  
  open import SessionType message
  open import Session message
  open import Transitions message
  
  private
    U : Set
    U = SessionType Ã— SessionType

  data FCompIS-RN : Set where
    client-end : FCompIS-RN
    oi io : FCompIS-RN

  data FCompCOIS-RN : Set where
    co-oi co-io : FCompCOIS-RN

  client-end-r : FinMetaRule U
  client-end-r .Ctx = Î£[ (S , T) âˆˆ SessionType Ã— SessionType ] Win S Ã— Defined T
  client-end-r .comp ((S , T) , _) =
    [] ,
    --------------------
    (S , T)

  OI-r : MetaRule U
  OI-r .Ctx = Î£[ (f , _) âˆˆ Continuation Ã— Continuation ] Witness f
  OI-r .Pos ((f , _) , _) = Î£[ t âˆˆ ğ•‹ ] t âˆˆ dom f
  OI-r .prems ((f , g) , _) (t , _) = f t .force , g t .force
  OI-r .conclu ((f , g) , _) = out f , inp g

  IO-r : MetaRule U
  IO-r .Ctx = Î£[ (_ , g) âˆˆ Continuation Ã— Continuation ] Witness g
  IO-r .Pos ((_ , g) , _) = Î£[ t âˆˆ ğ•‹ ] t âˆˆ dom g
  IO-r .prems ((f , g) , _) (t , _) = f t .force , g t .force
  IO-r .conclu ((f , g) , _) = inp f , out g

  co-OI-r : FinMetaRule U
  co-OI-r .Ctx = Î£[ (f , _ , x) âˆˆ Continuation Ã— Continuation Ã— ğ•‹ ] x âˆˆ dom f
  co-OI-r .comp ((f , g , x) , _) =
    (f x .force , g x .force) âˆ· [] ,
    --------------------
    (out f , inp g)

  co-IO-r : FinMetaRule U
  co-IO-r .Ctx = Î£[ (_ , g , x) âˆˆ Continuation Ã— Continuation Ã— ğ•‹ ] x âˆˆ dom g
  co-IO-r .comp ((f , g , x) , _) =
    (f x .force , g x .force) âˆ· [] ,
    --------------------
    (inp f , out g)

  FCompIS : IS U
  FCompIS .Names = FCompIS-RN
  FCompIS .rules client-end = from client-end-r
  FCompIS .rules oi = OI-r
  FCompIS .rules io = IO-r

  FCompCOIS : IS U
  FCompCOIS .Names = FCompCOIS-RN
  FCompCOIS .rules co-oi = from co-OI-r
  FCompCOIS .rules co-io = from co-IO-r

  _âŠ¢_ : SessionType â†’ SessionType â†’ Set
  S âŠ¢ T = FCoIndâŸ¦ FCompIS , FCompCOIS âŸ§ (S , T)

  _âŠ¢áµ¢_ : SessionType â†’ SessionType â†’ Set
  S âŠ¢áµ¢ T = IndâŸ¦ FCompIS âˆª FCompCOIS âŸ§ (S , T)

  {- Properties -}

  Â¬nil-âŠ¢ : âˆ€{S} â†’ Â¬ (nil âŠ¢ S)
  Â¬nil-âŠ¢ fc with fc .CoIndâŸ¦_âŸ§.unfold
  ... | client-end , ((_ , (() , _)) , _) , refl , _

  âŠ¢áµ¢->succeed : âˆ€{S T} â†’ S âŠ¢áµ¢ T â†’ MaySucceed (S # T)
  âŠ¢áµ¢->succeed (fold (injâ‚ client-end , ((S , T) , (win , def)) , refl , _)) = (S # T) , Îµ , win#def win def
  âŠ¢áµ¢->succeed (fold (injâ‚ oi , ((f , g) , (t , ok)) , refl , pr)) =
    let Sys' , red-Sys' , Succ = âŠ¢áµ¢->succeed (pr (t , ok)) in
    Sys' , sync (out ok) inp â—… red-Sys' , Succ
  âŠ¢áµ¢->succeed (fold (injâ‚ io , ((f , g) , (t , ok)) , refl , pr)) =
    let Sys' , red-Sys' , Succ = âŠ¢áµ¢->succeed (pr (t , ok)) in
    Sys' , sync inp (out ok) â—… red-Sys' , Succ
  âŠ¢áµ¢->succeed (fold (injâ‚‚ co-oi , (_ , ok-b) , refl , pr)) =
    let Sys' , red-Sys' , Succ = âŠ¢áµ¢->succeed (pr zero) in
    Sys' , sync (out ok-b) inp â—… red-Sys' , Succ
  âŠ¢áµ¢->succeed (fold (injâ‚‚ co-io , (_ , ok-b) , refl , pr)) =
    let Sys' , red-Sys' , Succ = âŠ¢áµ¢->succeed (pr zero) in
    Sys' , sync inp (out ok-b) â—… red-Sys' , Succ

  maysucceed->âŠ¢áµ¢ : âˆ€{S T Sys} â†’ Reductions (S # T) Sys â†’ Success Sys â†’ S âŠ¢áµ¢ T
  maysucceed->âŠ¢áµ¢ Îµ (win#def win def) = apply-ind (injâ‚ client-end) (_ , (win , def)) Î» ()
  maysucceed->âŠ¢áµ¢ (sync (out ok) inp â—… red) Succ =
    let rec = maysucceed->âŠ¢áµ¢ red Succ in
    apply-ind (injâ‚‚ co-oi) (_ , ok) Î»{zero â†’ rec}
  maysucceed->âŠ¢áµ¢ (sync inp (out ok) â—… red) Succ =
    let rec = maysucceed->âŠ¢áµ¢ red Succ in
    apply-ind (injâ‚‚ co-io) (_ , ok) Î»{zero â†’ rec}

  {- Soundness -}

  fc-sound : âˆ€{S T} â†’ S âŠ¢ T â†’ FairComplianceS (S # T)
  fc-sound fc Îµ = âŠ¢áµ¢->succeed (fcoind-to-ind fc)
  fc-sound fc (x â—… red) with fc .CoIndâŸ¦_âŸ§.unfold
  ... | client-end , ((_ , (win , def)) , _) , refl , _ = âŠ¥-elim (success-not-reduce (win#def win def) x)
  fc-sound fc (sync (out ok) (inp {x = t}) â—… red) | oi , (((f , _) , _) , _) , refl , pr = fc-sound (pr (t , ok)) red
  fc-sound fc (sync (inp {x = t}) (out ok) â—… red) | io , (((_ , g) , _) , _) , refl , pr = fc-sound (pr (t , ok)) red

  {- Boundedness -}

  fc-bounded : âˆ€{S T} â†’ FairComplianceS (S # T) â†’ S âŠ¢áµ¢ T
  fc-bounded fc =
    let _ , red-Sys , Succ = fc Îµ in
    maysucceed->âŠ¢áµ¢ red-Sys Succ
    
  {- Consistency -}

  fc-consistent : âˆ€{S T} â†’ FairComplianceS (S # T) â†’ ISF[ FCompIS ] (Î»{(S , T) â†’ FairComplianceS (S # T)}) (S , T)
  fc-consistent fc with fc Îµ
  ... | _ , Îµ , win#def win def = client-end , (_ , (win , def)) , refl , Î» ()
  ... | _ , sync (out ok) (inp {x = t}) â—… _ , _ = 
    oi , (_ , (t , ok)) , refl , Î» (t' , ok') red â†’ fc (sync (out ok') (inp {x = t'}) â—… red)
  ... | _ , sync (inp {x = t}) (out ok) â—… _ , _ =
    io , (_ , (t , ok)) , refl , Î» (t' , ok') red â†’ fc (sync (inp {x = t'}) (out ok') â—… red)

  {- Completeness -}
  
  fc-complete : âˆ€{S T} â†’ FairComplianceS (S # T) â†’ S âŠ¢ T
  fc-complete =
    let S = Î»{(S , T) â†’ FairComplianceS (S # T)} in
    bounded-coind[ FCompIS , FCompCOIS ] S fc-bounded fc-consistent

  
  {- Properties -}

  fci->defined : âˆ€{S T} â†’ S âŠ¢áµ¢ T â†’ Defined S Ã— Defined T
  fci->defined (fold (injâ‚ client-end , (_ , (out _ , def)) , refl , _)) = out , def
  fci->defined (fold (injâ‚ oi , _ , refl , _)) = out , inp
  fci->defined (fold (injâ‚ io , _ , refl , _)) = inp , out
  fci->defined (fold (injâ‚‚ co-oi , _ , refl , _)) = out , inp
  fci->defined (fold (injâ‚‚ co-io , _ , refl , _)) = inp , out

  fc->defined : âˆ€{S T} â†’ S âŠ¢ T â†’ Defined S Ã— Defined T
  fc->defined fc = fci->defined (fcoind-to-ind fc)

  no-fc-with-nil : âˆ€{S} â†’ Â¬ (S âŠ¢ nil)
  no-fc-with-nil fc with fc .CoIndâŸ¦_âŸ§.unfold
  ... | client-end , ((_ , (_ , ())) , _) , refl , _

  {- Auxiliary for FS -}

  winâŠ¢def : âˆ€{S} â†’ Defined S â†’ win âŠ¢ S
  winâŠ¢def ok = apply-fcoind client-end (_ , (Win-win , ok)) Î» ()

  âŠ¢-after-out : âˆ€{f g t} â†’ t âˆˆ dom f â†’ out f âŠ¢ inp g â†’ f t .force âŠ¢ g t .force
  âŠ¢-after-out ok fc with fc .CoIndâŸ¦_âŸ§.unfold
  ... | client-end , ((_ , (out e , _)) , _) , refl , _ = âŠ¥-elim (e _ ok)
  ... | oi , _ , refl , pr = pr (_ , ok)

  âŠ¢-after-in : âˆ€{f g t} â†’ t âˆˆ dom g â†’ inp f âŠ¢ out g â†’ f t .force âŠ¢ g t .force
  âŠ¢-after-in ok fc with fc .CoIndâŸ¦_âŸ§.unfold
  ... | client-end , ((_ , (() , _)) , _) , refl , _
  ... | io , _ , refl , pr = pr (_ , ok)